PREFIX pubo:  <http://lod.semanticsoftware.info/pubo/pubo#>
PREFIX sro: <http://salt.semanticauthoring.org/ontologies/sro#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX um: <http://intelleo.eu/ontologies/user-model/ns/>
PREFIX c: <http://www.intelleo.eu/ontologies/competences/ns/>
PREFIX foaf:  <http://xmlns.com/foaf/0.1/>
PREFIX local: <http://tmp/peerj-cs-100/>
PREFIX profile: <http://semanticsoftware.info/lodexporter/author/>

SELECT ?uri ?comment ?wikipedia WHERE {
  local:cs-78.xml  pubo:hasAnnotation ?rhetEntity.
  ?rhetEntity rdf:type sro:Contribution.
  ?rhetEntity pubo:containsNE ?namedEntity.
  ?namedEntity rdfs:isDefinedBy  ?uri.

  FILTER NOT EXISTS {
	?user rdf:type um:User.
  	?user rdfs:isDefinedBy profile:R4 .
  	?user um:hasCompetencyRecord ?comptenceRecord.
  	?comptenceRecord  c:competenceFor ?comptence.
  	?comptence rdfs:isDefinedBy ?uri.
  } 
  
   SERVICE <http://dbpedia.org/sparql> {
       ?uri rdfs:comment ?comment.
       ?uri foaf:isPrimaryTopicOf ?wikipedia
   }
   
   FILTER (LANGMATCHES(LANG(?comment), "en"))
}
